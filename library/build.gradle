apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply from: rootProject.file('config/version.gradle')
apply from: rootProject.file('config/dependancy.gradle')
apply from: rootProject.file('config/travis.gradle')
apply from: rootProject.file('config/publish/config.gradle')

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion ver.compileSdkVersion

    defaultConfig {
        minSdkVersion ver.minSdkVersion
        targetSdkVersion ver.targetSdkVersion
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    rootProject.file('proguard/default.pro'),
                    rootProject.file('proguard/kotlin.pro'),
                    rootProject.file('proguard/retrofit.pro')
        }
    }

}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs/javadoc"
}

// Fix javadoc and sources file names
archivesBaseName = bintrayName
// Fix AAR file name
android.libraryVariants.all { variant ->
    variant.outputs.all { output ->
        if (outputFile != null && outputFile.name.endsWith('.aar')) {
            outputFileName = "${bintrayName}-${libraryVersion}-${variant.name}.aar"
        }
    }
}

dependencies {
    implementation dep.kotlin
    implementation dep.supportAnnotations
    // REST client
    implementation dep.retrofit
    implementation dep.retrofitGson
    debugImplementation dep.retrofitLogging

    testImplementation test.junit
    testImplementation test.mockito
    testImplementation test.mockitoKotlin
    testImplementation test.kotlinReflect
}

apply from: rootProject.file('config/publish/maven.gradle')
apply from: rootProject.file('config/publish/bintray.gradle')
